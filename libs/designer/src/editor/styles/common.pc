
import "./theme.pc" as theme

public style panel {
  background: var(theme.sidebarColor)
  border: 1px solid var(theme.borderColor)
  border-radius: var(theme.roundedMD)
}

public component Sidebar {
  render div {
    style {
      width: 300px
      flex-shrink: 0
      display: flex
      flex-direction: column
      gap: var(theme.space05)
      padding: var(theme.space05)
      overflow: scroll 
    }
      slot children
  }
}

public component SidebarPanel {
  render div {
    style extends panel {
      flex: 1
    }
    slot children
  }
}


public component SidebarSection {
  variant first trigger {
    ":first-child"
  }
  render div {
    style {
      border-top: 1px solid var(theme.borderColor)
    }
    style variant first {
      border-top: none
    }
    slot children
  }
}

component SidebarPanelHeader {
  render div {
    style extends theme.fontHeader {
      padding: var(theme.space05)
      padding-bottom: 0px

    }
    div {
      style {
        display: flex
        justify-content: space-between

      }

      slot children
    }
  }
}

component SidebarPanelContent {
  variant flush trigger { ".flush" }
  render div(class:class) {
    style {
      padding: var(theme.space05)
    }
    style variant flush {
      padding: var(theme.space05) 0px
    }
    slot children
  }
}


component SidebarHeader {
  render div {
    style {
      display: flex
    }
    slot children
  }
}

component SidebarHeaderButton {
  variant selected
  render div root {
    style extends theme.easeIn {
      padding: var(theme.space03) var(theme.space05)
      font-size: var(theme.fontSize01)
      text-align: center
      width: 100%
      border-bottom: 2px solid transparent
    }
    style variant selected {
      border-color: var(theme.selectedColor)
    }
    slot children
  }
}