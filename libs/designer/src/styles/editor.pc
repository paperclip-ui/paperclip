import "./theme.pc" as theme
import "./left-sidebar.pc" as leftSidebar
import "./common.pc" as common
import "./right-sidebar.pc" as rightSidebar
import "./center-panels.pc" as centerPanels














token iconSize var(theme.fontSize03)
component NodeIcon {
    render div
}

public component Artboard {
    render div(style: style) {
        div {
            style {
                color: var(theme.fontColor01)
                position: absolute
                transform: translateY(calc(-100% - 0.5em))
            }
            slot title
        }
        slot children
    }
}

component ArtboardPreview {
    render div {
        style {
            width: 400px
            height: 300px
            background: white
        }
    }
}

public component Canvas {
    render div(style: style, onMouseMove: onMouseMove, onScroll: onScroll, onWheelCapture: onWheelCapture, tabIndex: "0") {
        style {
            flex: 1
            overflow: hidden
            position: absolute
            top: 0px
            left: 0px
            width: 100%
            height: 100%
        }
        slot children
    }
}

public component CanvasInner {
    render div(style: style, class: class) {
        style extends canvas {
            width: 100%
            height: 100%
        }
        slot children
    }
}

public component EditorPanels {
    render div {
        style {
            display: flex
            flex: 1
            z-index: 3
            gap: var(theme.space05)
            position: relative
            height: 100%
            justify-content: space-between
            pointer-events: none
        }
        slot children
    }
}

component FooterBar {
    render div {
        style extends panel {
            width: 100%
            height: 30px
        }
    }
}

public component Editor {
    render div {
        style extends theme.fontDefault {
            width: 100vw
            height: 100vh
            background: var(theme.canvasColor)
        }
        slot children
    }
}

/**
 * @bounds(x: 10, y: 0, width: 1384, height: 1166)
*/

Editor {
    Canvas {
        CanvasInner {
            Artboard(style: "left: 50%; top: 50%; position: absolute; transform: translate(-50%, -50%);") {
                insert title {
                    text "this should work!"
                }
                ArtboardPreview
            }
        }
    }
    EditorPanels panels {
        leftSidebar.LeftSidebarPreview leftSidebar
        centerPanels.Preview centerPanels
        rightSidebar.Preview
    }
}

/**
 * @bounds(x: 32, y: -859, width: 777, height: 495)
*/

div {
    style {
        position: relative
        display: flex
    }
}

