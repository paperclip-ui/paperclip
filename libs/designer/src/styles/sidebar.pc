
import "./theme.pc" as theme

public style panel extends theme.card

public component Sidebar {
  render div root {
    style extends theme.fontDefault {
      width: 300px
      flex-shrink: 0
      display: flex
      flex-direction: column
      gap: var(theme.space05)
      padding: var(theme.space05)
      pointer-events: all
    }
    slot children
  }
}


public component SidebarPanel {
  render div {
    style extends panel {
      flex: 1
      height: 100%
      overflow: hidden
      display: flex
      flex-direction: column
    }
    slot children
  }
}


public component SidebarSection {
  variant first trigger {
    ":first-child"
  }
  render div root {
    style {
      border-top: 1px solid var(theme.borderColor)
      overflow: hidden
      display: flex
      flex-direction: column
      overflow: hidden
      height: 100%
    }
    style variant first {
      border-top: none
    }
    slot children
  }
}

public component SidebarPanelHeader {
  render div {
    style extends theme.fontHeader {
      padding: var(theme.space05)

    }
    div {
      style {
        display: flex
        justify-content: space-between

      }

      slot children
    }
  }
}

public component SidebarPanelContent {
  variant flush trigger { ".flush" }
  variant scroll trigger { ".scroll" }
  render div(class:class) {
    style {
      padding: var(theme.space05)
    }
    style variant scroll {
      height: 100%
      overflow: scroll
    }
    style variant flush {
      padding: var(theme.space05) 0px
    }
    slot children
  }
}


public component SidebarHeader {
  render div {
    style {
      display: flex
    }
    slot children
  }
}

public component SidebarHeaderButton {
  variant selected
  render div root {
    style extends theme.easeIn {
      padding: var(theme.space03) var(theme.space05)
      font-size: var(theme.fontSize01)
      text-align: center
      width: 100%
      border-bottom: 2px solid transparent
    }
    style variant selected {
      border-color: var(theme.selectedColor)
    }
    slot children
  }
}

public component SidebarPopup {
  render div {
    style extends panel {
        position: fixed
        flex-shrink: 0
        transform: translate(calc(-100% - var(theme.space07)))
        z-index: 1024
        min-width: 200px
    }
    div {
      style {
          padding: var(theme.space04)
          display: flex
          justify-content: space-between
          font-weight: 600
          padding-bottom: var(theme.space04)
      }
      slot header

      button(onClick: onCloseClick) {
        style extends theme.reset {
          cursor: pointer
        }
        text "Ã—"
      }
    }
    div {
      slot children
    }
  }
}

public component SidebarPopupPanelContent {
  render div {
      style {
        padding: var(theme.space04)
      }
      slot children
  }
}