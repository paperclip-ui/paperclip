import "./theme.pc" as theme

public style panel extends theme.card

public component SidebarContainer {
    script(src: "../components/Sidebar/index.tsx", target: "react")
    variant right trigger {
        ".right"
    }
    variant left trigger {
        ".left"
    }
    render div(class: class, style: style) {
        style {
            display: flex
            padding: var(theme.space05)
            pointer-events: all
            flex-shrink: 0
        }
        style variant left {
            flex-direction: row
        }
        style variant right {
            flex-direction: row-reverse
        }
        slot children {
            Sidebar
        }
        div sidebar(onMouseDown: onSidebarMouseDown) {
            style {
                background: transparent
                position: relative
                width: 4px
                height: 100%
                cursor: ew-resize
                flex-shrink: 0
            }
        }
    }
}

/**
 * @bounds(x: 1329, y: -71, width: 243, height: 768)
*/

public component Sidebar {
    render div root(class: class, style: style) {
        style extends theme.fontDefault {
            width: 100%
            flex-shrink: 0
            display: flex
            flex-direction: row
            gap: 0px
            padding: 0px
            pointer-events: all
        }
        div panels {
            style {
                height: 100%
                width: 100%
                display: flex
                flex-direction: column
                gap: var(theme.space05)
            }
            slot children {
                SidebarPanel {
                    text "panel 2"
                }
                SidebarPanel {
                    text "panel 2"
                }
            }
        }
    }
}

/**
 * @bounds(x: 1598, y: -49, width: 189, height: 369)
*/

public component SidebarPanel {
    render div {
        style extends panel {
            flex: 1
            height: 100%
            overflow: scroll
            display: flex
            flex-direction: column
        }
        slot children
        div {
            style {
                z-index: -1
                display: none
                filter: blur(8px)
                background: var(theme.gray06)
                width: 100%
                height: 100%
                position: absolute
                left: 0px
                top: 0px
            }
        }
    }
}

public component SidebarSection {
    variant fill trigger {
        ".fill"
    }
    variant first trigger {
        ":first-child"
    }
    render div root(class: class) {
        style {
            border-top: 1px solid var(theme.borderColor)
            display: flex
            flex-direction: column
            width: 100%
        }
        style variant fill {
            height: 100%
            overflow: hidden
        }
        style variant first {
            border-top: none
        }
        slot children
    }
}

/**
 * @bounds(x: 325, y: 958, width: 1024, height: 768)
*/

public component SidebarPanelHeader {
    render div {
        style extends theme.fontHeader {
            padding: var(theme.space05)
            width: 100%
        }
        div {
            style {
                display: flex
                justify-content: space-between
                align-items: center
                gap: var(theme.space03)
                width: 100%
            }
            slot children
        }
    }
}

public component SidebarPanelContent {
    variant scroll trigger {
        ".scroll"
    }
    render div(class: class) {
        style {
            padding: var(theme.space05)
        }
        style variant scroll {
            height: 100%
            overflow: scroll
        }
        style variant flush {
            padding: var(theme.space05) 0px
        }
        slot children
    }
}

public component SidebarHeader {
    render div {
        style {
            display: flex
        }
        slot children
    }
}

public component SidebarHeaderButton {
    variant selected
    render div root {
        style extends theme.easeIn {
            padding: var(theme.space03) var(theme.space05)
            font-size: var(theme.fontSize01)
            text-align: center
            width: 100%
            border-bottom: 2px solid transparent
        }
        style variant selected {
            border-color: var(theme.selectedColor)
        }
        slot children
    }
}

/**
 * @bounds(x: 93, y: 904, width: 378, height: 267)
*/

public component SidebarPopup {
    render div(style: style) {
        style extends panel {
            position: fixed
            flex-shrink: 0
            z-index: 1024
            min-width: 200px
            font-family: var(theme.defaultFontFamily)
            color: var(theme.fontColor01)
            font-size: var(theme.fontSize01)
            transform: translate(calc(-100% - var(theme.space07)))
        }
        div {
            style {
                padding: var(theme.space04)
                display: flex
                justify-content: space-between
                font-weight: 600
                padding-bottom: var(theme.space04)
            }
            slot header {
                text "title"
            }
            button(onClick: onCloseClick) {
                style extends theme.reset {
                    cursor: pointer
                }
                text "Ã—"
            }
        }
        div {
            slot children
        }
    }
}

public component SidebarPopupPanelContent {
    render div {
        style {
            padding: var(theme.space04)
        }
        slot children
    }
}

/**
 * @bounds(x: 1069, y: 86, width: 189, height: 162)
*/

div {
    style {
        position: relative
    }
    div {
        style {
            background: rgba(0, 0, 0, 0.1)
            position: absolute
            left: 46px
            top: 51px
            width: 124px
            height: 83px
        }
    }
}
