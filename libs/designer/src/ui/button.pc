import "ui/theme.pc" as module
import "./theme.pc" as mod



/**
 * @frame(x: 962, y: 167, width: 144, height: 39)
*/
public component Button {
    variant disabled trigger {
        ".disabled"
        ":disabled"
    }
    variant small trigger {
        ".small"
    }
    variant hover trigger {
        ":hover"
        ".hover"
    }
    variant danger trigger {
        ".danger"
    }
    variant secondary trigger {
        ".secondary"
    }
    variant warning trigger {
        ".warning"
    }
    render button root(class: class) {
        style variant disabled {
            opacity: 0.5
            cursor: not-allowed
            pointer-events: none
        }
        style variant small {
            padding: var(mod.space03) var(mod.space05)
            font-size: var(module.fontSize01)
        }
        style variant secondary + hover {
            color: var(mod.blue00)
            border-color: var(mod.blue00)
        }
        style variant hover {
            background: var(mod.blue00)
        }
        style variant secondary {
            background: transparent
            border-color: var(mod.blue01)
            color: var(mod.blue01)
        }
        style variant danger {
            background: red
            color: maroon
        }
        style {
            position: relative
            background: var(mod.blue01)
            border: 1px solid var(mod.blue01)
            border-radius: var(mod.space02)
            padding: var(mod.space04) var(mod.space06)
            color: var(mod.fontColor01)
            cursor: pointer
            transition: 0.2s
        }
        slot children {
            text "a button"
        }
    }
}

/**
 * @frame(x: 960, y: -56, width: 458, height: 174)
*/
div preview {
    style {
        position: relative
        display: flex
        gap: var(mod.space02)
    }
    div {
        style {
            display: inline-flex
            align-items: flex-start
            gap: var(mod.space03)
            cursor: pointer
            flex-wrap: wrap
        }
        Button {
            insert children {
                text "primary"
            }
        }
        Button(class: "secondary") {
            insert children {
                text "secondary"
            }
        }
        Button {
            insert children {
                text "warning (todo)"
            }
        }
        Button {
            insert children {
                text "danger (todo)"
            }
        }
        Button(class: "hover") {
            insert children {
                text "primary:hover"
            }
        }
        Button(class: "secondary hover") {
            insert children {
                text "secondary:hover"
            }
        }
        Button(class: "small") {
            insert children {
                text "primary small"
            }
        }
        Button(class: "small disabled") {
            insert children {
                text "disabled primary small"
            }
        }
    }
}

