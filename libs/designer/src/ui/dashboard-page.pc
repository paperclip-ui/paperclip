import "./input.pc" as input
import "./theme.pc" as theme
import "./etc.pc" as etc

/**
 * @bounds(x: 290, y: 329, width: 1024, height: 768)
*/
public component Container {
    variant smallScreen trigger {
        "@media screen and (min-width: 0px)"
    }
    variant medScreen trigger {
        "@media screen and (min-width: 700px)"
    }
    variant largeScreen trigger {
        "@media screen and (min-width: 800px)"
    }
    variant xlargeScreen trigger {
        "@media screen and (min-width: 1200px)"
    }
    render div {
        style extends theme.fontDefault {
            position: relative
            background: var(theme.gray06)
            width: 100vw
            height: 100vh
            display: flex
            flex-direction: column
        }
        div header {
            style {
                display: flex
                align-items: center
            }
            h1 {
                style extends theme.fontDefault {
                    margin: 0px
                    font-size: 18px
                    padding: var(theme.space06)
                }
                text "Design files"
            }
            div controls {
                style {
                    max-width: 200px
                    display: flex
                    align-items: center
                    gap: var(theme.space06)
                }
                slot controls {
                    input.TextInput(placeholder: "Filter...", value: "")
                    AddFileButton
                }
            }
        }
        div items {
            style {
                padding: var(theme.space06)
                display: grid
                gap: var(theme.space05)
                overflow: scroll
            }
            style variant smallScreen {
                grid-template-columns: 1fr 1fr
            }
            style variant medScreen {
                grid-template-columns: 1fr 1fr 1fr
            }
            style variant largeScreen {
                grid-template-columns: 1fr 1fr 1fr 1fr
            }
            style variant xlargeScreen {
                grid-template-columns: 1fr 1fr 1fr 1fr 1fr
            }
            slot items {
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
                etc.Item
            }
        }
    }
}

public component AddFileButton {
    render button(onClick: onAddClick) {
        style extends theme.reset, theme.icon {
            -webkit-mask-image: url("./plus2.svg")
            cursor: pointer
        }
        text "something"
    }
}

