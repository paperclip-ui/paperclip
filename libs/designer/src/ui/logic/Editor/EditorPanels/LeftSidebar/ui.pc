import "state/theme.pc" as theme1
import "ui/input.pc" as module
import "ui/logic/Sidebar/sidebar.pc" as sidebar
import "ui/theme.pc" as theme
import "ui/etc.pc" as etc

/**
 * @frame(x: 258, y: 1039, width: 347, height: 252)
*/
public component FilteredFiles {
    script(src: "./FileNavigator/controller.tsx", target: "react", name: "FilteredFiles")
    render div filteredFiles {
        slot children {
            FilteredFile(class: "preselected component") {
                insert basename {
                    text "some long base name that should work"
                }
                insert dirname {
                    text "./path/to/some/dir"
                }
            }
            FilteredFile(class: "file")
            FilteredFile(class: "atom-token") {
                insert basename {
                    text "file.svg"
                }
                insert dirname {
                    text "./path/to/some/dir"
                }
            }
            FilteredFile(class: "composite-token") {
                insert basename {
                    text "file.svg"
                }
                insert dirname {
                    text "./path/to/some/dir"
                }
            }
            FilteredFile(class: "component") {
                insert basename {
                    text "file.svg"
                }
                insert dirname {
                    text "./a/super/loooooooooooooooooo00000000000000000ong-dir"
                }
            }
        }
        style {
            gap: var(theme.space02)
            display: flex
            flex-direction: column
            font-family: var(theme.defaultFontFamily)
            color: var(theme.gray0)
        }
    }
}

/**
 * @frame(x: 266, y: 1343, width: 354, height: 88)
*/
public component FilteredFile {
    variant isStyleMixin trigger {
        ".composite-token"
        ".style-mixin"
    }
    variant isToken trigger {
        ".token"
        ".atom-token"
    }
    variant isFile trigger {
        ".file"
    }
    variant isComponent trigger {
        ".component"
    }
    variant hovering trigger {
        ".hover"
        ":hover"
        ".preselected"
    }
    script(src: "./FileNavigator/controller.tsx", target: "react", name: "FilteredFile")
    render div container(class: class, style: style) {
        style variant isStyleMixin {
            color: var(theme.atomColor)
        }
        style variant isToken {
            color: var(theme.atomColor)
        }
        style {
            display: flex
            align-items: center
            gap: var(theme.space05)
            cursor: pointer
            background: transparent
        }
        div container {
            style variant hovering {
                background: var(theme.gray04)
            }
            style {
                display: flex
                justify-content: space-between
                width: 100%
                padding: var(theme.space03) var(theme.space05)
            }
            div icon {
                style {
                    width: 1em
                    height: 1em
                    background: red
                    flex-shrink: 0
                    border-radius: 4px
                    display: none
                }
            }
            div info {
                style {
                    display: flex
                    justify-content: space-between
                    width: 100%
                    gap: var(theme.space05)
                    align-items: center
                }
                div {
                    div preview {
                        div {
                            style {
                                background: url("ui/lightning.svg")
                                background-size: 100%
                                width: 100%
                                height: 100%
                            }
                        }
                        style {
                            width: 25px
                            height: 25px
                            background: white
                            border-radius: var(theme1.roundedSM)
                            flex-shrink: 0
                            position: relative
                            padding: var(theme1.space01)
                        }
                    }
                    style {
                        display: flex
                        align-items: center
                        gap: var(theme1.space03)
                    }
                    div iconWraper {
                        style variant isComponent {
                            color: var(theme.componentColor)
                        }
                        style variant isStyleMixin
                        LayerIcon(class: class)
                    }
                    div basenameContainer {
                        style {
                            white-space: nowrap
                            text-overflow: ellipsis
                            overflow: hidden
                        }
                        slot basename {
                            text "file.pc"
                        }
                    }
                }
                div dirnameContainer(title: dirTitle) {
                    style {
                        opacity: 0.5
                        overflow: hidden
                        text-overflow: ellipsis
                        white-space: nowrap
                    }
                    slot dirname {
                        text "./some/dir"
                    }
                }
            }
        }
    }
}

/**
 * @frame(x: 1787, y: 217, width: 225, height: 54)
*/
public component FSItem {
    script(src: "./FileNavigator/controller.tsx", target: "react", name: "FSNavigatorItem")
    render TreeNavigationItem {
        LayerNavigationItemHeader layerHeader(class: headerClass) {
            slot header {
                text "header"
            }
        }
        TreeNavigationItemContent {
            slot children
        }
    }
}

/**
 * @frame(x: -2102, y: -298, width: 380, height: 768)
*/
public component FileNavigator {
    script(src: "./FileNavigator/controller.tsx", target: "react", name: "FileNavigator")
    render sidebar.SidebarPanel {
        sidebar.SidebarSection(class: "fill") {
            sidebar.SidebarPanelHeader {
                style {
                    align-items: center
                    display: flex
                }
                slot header {
                    text "Resource"
                    module.TextInput(value: "", placeholder: "filter...")
                    div {
                        style extends theme.icon {
                            -webkit-mask-image: url("../../../..//plus.svg")
                        }
                    }
                }
            }
            div {
                style {
                    overflow-y: scroll
                    overflow-x: hidden
                }
                slot layers {
                    FSItem(headerClass: "folder container open") {
                        insert header {
                            text "components/"
                        }
                        insert children {
                            TreeNavigationItem {
                                LayerNavigationItemHeader(class: "file open") {
                                    text "index.tsx"
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

/**
 * @frame(x: 250, y: 180, width: 337, height: 768)
*/
component FileResources {
    render sidebar.SidebarPanel {
        sidebar.SidebarSection {
            sidebar.SidebarPanelHeader {
                style {
                    align-items: center
                    display: flex
                }
                text "Files"
                module.TextInput(value: "some file")
                div {
                    style extends theme.icon {
                        -webkit-mask-image: url("../../../../plus.svg")
                    }
                }
            }
            FilteredFiles
        }
    }
}

/**
 * @frame(x: 761, y: 197, width: 1024, height: 768)
*/
public component LeftSidebar {
    script(src: "./controller.tsx", target: "react", name: "LeftSidebar")
    render sidebar.SidebarContainer(position: "left") {
        sidebar.Sidebar {
            insert children {
                Layers layers
                FileNavigator fileNavigator
            }
        }
    }
}

/**
 * @frame(x: -1009, y: 946, width: 201, height: 52)
*/
component LeftSidebarHeader {
    render sidebar.SidebarPanelContent {
        style {
            display: flex
            justify-content: space-between
            align-items: center
            height: 56px
            font-weight: 600
        }
        div left {
            style {
                display: flex
                gap: 8px
                align-items: center
            }
            span titleContainer {
                slot title {
                    text "title"
                }
            }
        }
        div right {
            style {
                display: flex
                gap: 4px
            }
            div controls {
                style extends theme.icon {
                    cursor: pointer
                    -webkit-mask-image: url("../../../../hamburger.svg")
                    width: 1.2em
                    height: 1.2em
                    display: none
                }
            }
        }
    }
}

/**
 * @frame(x: 1999, y: 725, width: 27, height: 28)
*/
public component AddLayerButton {
    script(src: "./Layers/AddLayerButton.tsx", target: "react", name: "AddLayerButton")
    render etc.PlusButton(onClick: onClick, class: class, onBlur: onBlur)
}

/**
 * @frame(x: -295, y: 172, width: 459, height: 768)
*/
public component Layers {
    script(src: "./Layers/controller.tsx", target: "react", name: "Layers")
    render sidebar.SidebarPanel sidebar {
        LeftSidebarHeader header {
            insert title {
                slot title {
                    text "TodoList.pc"
                }
            }
        }
        sidebar.SidebarSection entities(class: "fill") {
            sidebar.SidebarPanelHeader entitiesHeader {
                text "Entities"
                AddLayerButton
            }
            div {
                style {
                    padding: var(theme.space03) 0px
                    overflow-y: scroll
                    overflow-x: hidden
                }
                slot layers {
                    TreeNavigationItem {
                        LayerNavigationItemHeader(class: "instance container open") {
                            text "Some instance"
                        }
                        LayerNavigationItemHeader(class: "cutting component container open") {
                            text "Some component"
                            text "some blarggggfgdf gdfgd"
                        }
                        TreeNavigationItemContent {
                            TreeNavigationItem {
                                LayerNavigationItemHeader(class: "shadow element container open showDropOver") {
                                    text "container"
                                }
                            }
                            TreeNavigationItem {
                                LayerNavigationItemHeader(class: "slot container open") {
                                    text "label"
                                }
                                TreeNavigationItemContent {
                                    TreeNavigationItem {
                                        LayerNavigationItemHeader(class: "text showDropTop") {
                                            text "some label"
                                        }
                                        LayerNavigationItemHeader(class: "element") {
                                            text "some long label"
                                            TagType {
                                                text "div"
                                            }
                                        }
                                    }
                                }
                                TreeNavigationItemContent {
                                    TreeNavigationItem
                                }
                            }
                            TreeNavigationItem {
                                LayerNavigationItemHeader(class: "selected slot container open") {
                                    text "description"
                                }
                                TreeNavigationItemContent {
                                    TreeNavigationItem {
                                        LayerNavigationItemHeader(class: "condition") {
                                            style {
                                                --depth: 3
                                            }
                                            text "some condition"
                                        }
                                        TreeNavigationItemContent {
                                            TreeNavigationItem {
                                                LayerNavigationItemHeader(class: "stack") {
                                                    style {
                                                        --depth: 4
                                                    }
                                                    text "some condition"
                                                }
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

/**
 * @frame(x: 808, y: 1052, width: 90, height: 56)
*/
public component LayerIcon {
    variant isCompositeToken trigger {
        ".composite-token"
    }
    variant isAtomToken trigger {
        ".atom-token"
    }
    variant isStack trigger {
        ".stack"
    }
    variant isCondition trigger {
        ".condition"
    }
    variant isComponent trigger {
        ".component"
    }
    variant isInstance trigger {
        ".instance"
    }
    variant isElement trigger {
        ".element"
    }
    variant isFolder trigger {
        ".folder"
    }
    variant isFile trigger {
        ".file"
    }
    variant isSlot trigger {
        ".slot"
    }
    variant isText trigger {
        ".text"
    }
    variant isTrigger trigger {
        ".trigger"
    }
    render div root(class: class) {
        style {
            width: 12px
            position: relative
            height: 1em
            flex-shrink: 0
        }
        div {
            style variant isStack {
                mask-image: url("./stack.png")
            }
            style variant isCondition {
                mask-image: url("./hexagon.png")
            }
            style extends theme.icon {
                position: absolute
                top: 50%
                left: 50%
                transform: translate(-50%, -50%)
            }
            style variant isComponent {
                width: 1.1em
                height: 1.1em
                -webkit-mask-image: url("../../../../component2.svg")
            }
            style variant isInstance {
                width: 0.9em
                height: 0.9em
                -webkit-mask-image: url("../../../../box-outline.svg")
                transform: translate(-50%, -50%) rotate(45deg)
            }
            style variant isSlot {
                -webkit-mask-image: url("../../../../slot3.svg")
            }
            style variant isElement {
                width: 0.75em
                height: 0.75em
                -webkit-mask-image: url("../../../../square.svg")
            }
            style variant isFolder {
                width: 0.9em
                height: 0.9em
                top: calc(50% + 1px)
                -webkit-mask-image: url("../../../../folder.svg")
            }
            style variant isFile {
                width: 0.9em
                height: 0.9em
                -webkit-mask-image: url("../../../../file.svg")
            }
            style variant isCompositeToken {
                width: 0.9em
                height: 0.9em
                -webkit-mask-image: url("../../../../group.svg")
            }
            style variant isAtomToken {
                width: 0.8em
                height: 0.8em
                -webkit-mask-image: url("../../../../dot4.svg")
            }
            style variant isTrigger {
                width: 1.1em
                height: 1.1em
                position: relative
                -webkit-mask-image: url("../../../../lightning.svg")
            }
            style variant isText {
                width: 0.8em
                height: 0.8em
                -webkit-mask-image: url("../../../../text.svg")
            }
        }
    }
}

/**
 * @frame(x: 428, y: -205, width: 1024, height: 768)
*/
public component TreeNavigationItemContent {
    render div {
        slot children
    }
}

/**
 * @frame(x: -453, y: 1092, width: 202, height: 112)
*/
public component TreeNavigationItem {
    render div(class: class, style: style) {
        slot children
    }
}

/**
 * @frame(x: -459, y: 1034, width: 93, height: 37)
*/
public component TagType {
    render span {
        style {
            opacity: 0.3
        }
        slot children
    }
}

/**
 * @frame(x: -1928, y: 3271, width: 1024, height: 768)
*/
public component DropZoneDivider {
    variant top trigger {
        ".top"
    }
    variant bottom trigger {
        ".bottom"
    }
    render div root(class: class) {
        style {
            background: var(theme.blue01)
            width: 100%
            height: 2px
            position: absolute
            transform: translateY(-50%)
            z-index: 1024
            display: none
        }
        style variant top {
            top: 0px
        }
        style variant bottom {
            bottom: 0px
            transform: translateY(50%)
        }
        div circle {
            style {
                width: 4px
                height: 4px
                position: absolute
                left: 0px
                top: 50%
                border-radius: 99px
                transform: translateY(-50%)
                background: var(theme.blue01)
            }
        }
    }
}

/**
 * @frame(x: -1036, y: 1079, width: 224, height: 38)
*/
public component TreeNavigationItemHeader {
    variant isContainer trigger {
        ".container"
    }
    variant isOpen trigger {
        ".open"
    }
    variant showDropTop trigger {
        ".showDropTop"
    }
    variant showDropBottom trigger {
        ".showDropBottom"
    }
    variant showDropOver trigger {
        ".showDropOver"
    }
    variant isHovering trigger {
        ":hover"
    }
    variant isSelected trigger {
        ".selected"
    }
    variant isShadow trigger {
        ".shadow"
    }
    render div root(class: class, style: style, onClick: onClick, onContextMenu: onContextMenu, onKeyDown: onKeyDown, onMouseDown: onMouseDown) {
        style {
            display: flex
            flex-direction: column
            padding-left: calc(22px * var(--depth, 1))
            position: relative
            cursor: pointer
            user-select: none
        }
        style variant showDropOver {
            box-shadow: inset 0px 0px 0px 2px var(theme.blue01)
        }
        style variant isShadow {
            background: rgb(31, 31, 31)
        }
        style variant isSelected {
            background: var(theme.gray04)
        }
        DropZoneDivider(class: "top")
        div {
            style {
                display: flex
                gap: var(theme.space02)
                justify-content: space-between
                padding: var(theme.space03) var(theme.space05)
            }
            div {
                style {
                    position: relative
                    display: flex
                    gap: var(theme.space03)
                    align-items: center
                    left: -1px
                    top: -1px
                    width: 239px
                    height: 15px
                }
                div(onClick: onArrowClick) {
                    style extends theme.icon {
                        -webkit-mask-image: url("../../../../chevron2.svg")
                        opacity: 0
                        color: white
                        width: 0.9em
                        height: 0.9em
                        position: absolute
                        left: -18px
                        transition: 0.2s
                        outline: none
                    }
                    style variant isContainer {
                        opacity: 0.3
                    }
                    style variant isOpen {
                        transform: rotate(90deg)
                    }
                }
                slot children {
                    text "label"
                }
            }
            div {
                style extends theme.easeIn {
                    opacity: 1
                    --controlOpacity: 0
                }
                style variant isHovering {
                    --controlOpacity: 0.5
                }
                slot controls
            }
        }
        DropZoneDivider(class: "bottom")
    }
}

/**
 * @frame(x: 795, y: 1167, width: 350, height: 37)
*/
public component LayerNavigationItemHeader {
    variant cutting trigger {
        ".cutting"
    }
    variant isStack trigger {
        ".stack"
    }
    variant isCondition trigger {
        ".condition"
    }
    variant isSlot trigger {
        ".slot"
    }
    variant isComponent trigger {
        ".component"
    }
    variant isInstance trigger {
        ".instance"
    }
    variant isElement trigger {
        ".element"
    }
    variant isCompositeToken trigger {
        ".composite-token"
    }
    variant isShadow trigger {
        ".shadow"
    }
    variant isAtomToken trigger {
        ".atom-token"
    }
    variant isTrigger trigger {
        ".trigger"
    }
    variant isText trigger {
        ".text"
    }
    render TreeNavigationItemHeader container(class: class, style: style, onClick: onClick, onArrowClick: onArrowClick, onContextMenu: onContextMenu, onKeyDown: onKeyDown, onMouseDown: onMouseDown) {
        style variant cutting {
            opacity: 0.3
        }
        style variant isStack {
            --icon-color: rgba(230, 158, 15, 1)
        }
        style variant isCondition {
            --icon-color: rgba(180, 240, 4, 1)
        }
        style {
            --regular-layer: rgb(96, 149, 255)
            white-space: nowrap
        }
        style variant isComponent {
            --icon-color: var(theme.componentColor)
        }
        style variant isInstance {
            --icon-color: rgb(219, 161, 255)
        }
        style variant isElement {
            --icon-color: var(--regular-layer)
        }
        style variant isCompositeToken {
            --icon-color: var(theme.atomColor)
        }
        style variant isAtomToken {
            --icon-color: rgb(114, 247, 134)
        }
        style variant isTrigger {
            --icon-color: rgb(114, 247, 134)
        }
        style variant isSlot {
            --icon-color: var(--regular-layer)
        }
        style variant isText {
            --icon-color: var(--regular-layer)
        }
        style variant isShadow {
            --icon-color: var(theme.componentColor)
        }
        LayerIcon(class: class) {
            style {
                color: var(--icon-color)
            }
        }
        slot children {
            text "some layer"
            text "some layer"
        }
        insert controls {
            slot controls
        }
    }
}

